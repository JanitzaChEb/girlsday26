#include <Arduino.h>
#include <Wire.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>
#include "SmartPlant.h"

// PINS
static constexpr uint8_t SENSOR = A0;
static constexpr uint8_t SENSOR_STROMVERSORGUNG = 7;
static constexpr uint8_t LED_ROT = 3;
static constexpr uint8_t LED_GELB = 5;
static constexpr uint8_t LED_GRUEN = 9;
static constexpr uint8_t LDR = A2;

// DISPLAY EINSTELLUNGEN
static constexpr int SCREEN_WIDTH = 128;
static constexpr int SCREEN_HEIGHT = 64;
static constexpr uint8_t OLED_ADDR = 0x3C;
Adafruit_SSD1306 display(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire, -1);

// Messwerte
int prozent_feuchtigkeit = 0;
int messwert_feuchtigkeit = 0;
int messwert_licht = 0;
int prozent_licht = 0;
// Statuswerte
bool zeigeFeuchtigkeit = false;
Emotion emotion = NEUTRAL;

// #######################################################################################################
static uint16_t g_frameCounter = 0;
static uint32_t g_lastFrameSwitchMs = 0;

static const uint8_t PROGMEM bmp_neutral_0[] = {
    0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x04, 0x04, 0x00, 0x38, 0x03, 0x80, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x18, 0x03, 0x00, 0x2c, 0x05, 0x80, 0x3c, 0x07, 0x80, 0x18, 0x03, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x01, 0xf0, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00};

static const uint8_t PROGMEM bmp_neutral_1[60] = {
    0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x04, 0x04, 0x00, 0x38, 0x03, 0x80, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x18, 0x03, 0x00, 0x34, 0x06, 0x80, 0x3c, 0x07, 0x80, 0x18, 0x03, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x01, 0xf0, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00};

static const uint8_t PROGMEM bmp_sad_0[60] = {
    0xFF, 0xFF, 0xF0, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10,
    0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10,
    0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10,
    0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10,
    0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0xFF, 0xFF, 0xF0};

static const uint8_t PROGMEM bmp_sad_1[60] = {
    0xFF, 0xFF, 0xF0, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10,
    0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10,
    0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10,
    0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10,
    0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0xFF, 0xFF, 0xF0};

static const uint8_t PROGMEM bmp_happy_0[60] = {
    0xFF, 0xFF, 0xF0, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10,
    0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10,
    0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10,
    0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10,
    0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0xFF, 0xFF, 0xF0};

static const uint8_t PROGMEM bmp_happy_1[60] = {
    0xFF, 0xFF, 0xF0, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10,
    0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10,
    0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10,
    0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10,
    0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0xFF, 0xFF, 0xF0};

static const uint8_t PROGMEM bmp_laugh_0[60] = {
    0xFF, 0xFF, 0xF0, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10,
    0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10,
    0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10,
    0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10,
    0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0xFF, 0xFF, 0xF0};

static const uint8_t PROGMEM bmp_laugh_1[60] = {
    0xFF, 0xFF, 0xF0, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10,
    0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10,
    0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10,
    0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0x80, 0x00, 0x10,
    0x80, 0x00, 0x10, 0x80, 0x00, 0x10, 0xFF, 0xFF, 0xF0};

static const uint8_t *const frames_neutral[] = {bmp_neutral_0, bmp_neutral_1};
static const uint8_t *const frames_sad[] = {bmp_sad_0, bmp_sad_1};
static const uint8_t *const frames_happy[] = {bmp_happy_0, bmp_happy_1};
static const uint8_t *const frames_laugh[] = {bmp_laugh_0, bmp_laugh_1};

static const Animation anim_neutral = {frames_neutral, 2, 250, 20, 20};
static const Animation anim_sad = {frames_sad, 2, 250, 20, 20};
static const Animation anim_happy = {frames_happy, 2, 200, 20, 20};
static const Animation anim_laugh = {frames_laugh, 2, 150, 20, 20};

static const Animation *animationFuerEmotion(Emotion e)
{
  switch (e)
  {
  case TRAURIG:
    return &anim_sad;
  case GLUECKLICH:
    return &anim_happy;
  case LACHT:
    return &anim_laugh;
  case NEUTRAL:
  default:
    return &anim_neutral;
  }
}

// #######################################################################################################

void initialisiereSmartPlant()
{
  Serial.begin(115200);

  pinMode(SENSOR_STROMVERSORGUNG, OUTPUT);
  pinMode(LED_ROT, OUTPUT);
  pinMode(LED_GELB, OUTPUT);
  pinMode(LED_GRUEN, OUTPUT);
  pinMode(SENSOR, INPUT);
  pinMode(LDR, INPUT);

  Wire.begin();

  if (!display.begin(SSD1306_SWITCHCAPVCC, OLED_ADDR))
  {
    boolean ledBlink = false;
    for (;;)
    {
      setzeLEDFarbe(ledBlink ? ROT : AUS);
      ledBlink = !ledBlink;
      delay(200);
    }
  }

  // Animation-Timer initialisieren
  g_frameCounter = 0;
  g_lastFrameSwitchMs = millis();
}

// #######################################################################################################

void setzeLEDFarbe(LED_Farbe farbe)
{
  if (farbe == ROT)
  {
    digitalWrite(LED_ROT, HIGH);
    digitalWrite(LED_GELB, LOW);
    digitalWrite(LED_GRUEN, LOW);
  }
  if (farbe == GELB)
  {
    digitalWrite(LED_ROT, LOW);
    digitalWrite(LED_GRUEN, LOW);
    digitalWrite(LED_GELB, HIGH);
  }
  if (farbe == AUS)
  {
    digitalWrite(LED_ROT, LOW);
    digitalWrite(LED_GELB, LOW);
    digitalWrite(LED_GRUEN, LOW);
  }
  if (farbe == GRUEN)
  {
    digitalWrite(LED_ROT, LOW);
    digitalWrite(LED_GELB, LOW);
    digitalWrite(LED_GRUEN, HIGH);
  }
}

int rechneMesswertInProzentUm(int raw, int messwert_min, int messwert_max)
{
  raw = constrain(raw, messwert_min, messwert_max);
  return map(raw, messwert_min, messwert_max, 0, 100);
}

int leseFeuchtigkeitswertInProzent(int messwert_trocken, int messwert_nass)
{
  digitalWrite(SENSOR_STROMVERSORGUNG, HIGH);
  delay(10);
  messwert_feuchtigkeit = analogRead(SENSOR);
  digitalWrite(SENSOR_STROMVERSORGUNG, LOW);
  prozent_feuchtigkeit = rechneMesswertInProzentUm(messwert_feuchtigkeit, messwert_nass, messwert_trocken);
  return prozent_feuchtigkeit;
}

int leseLichtInProzent(int messwert_dunkel, int messwert_hell)
{
  messwert_licht = analogRead(LDR);
  prozent_licht = rechneMesswertInProzentUm(messwert_licht, messwert_dunkel, messwert_hell);
  return prozent_licht;
}

void aktiviereFeuchtigkeitswertAufDisplay()
{
  zeigeFeuchtigkeit = true;
}

void setzeEmotion(Emotion neueEmotion)
{
  if (emotion != neueEmotion)
  {
    emotion = neueEmotion;
    g_frameCounter = 0;
    g_lastFrameSwitchMs = millis();
  }
}

void aktualisiereDisplay()
{
  display.clearDisplay();

  const Animation *anim = animationFuerEmotion(emotion);

  // Frame-Logik: alle frameDelayMs den Frame weiterschalten
  const uint32_t now = millis();
  if ((uint32_t)(now - g_lastFrameSwitchMs) >= anim->frameDelayMs)
  {
    g_lastFrameSwitchMs = now;
    g_frameCounter++;
  }

  const uint8_t frameIndex = (anim->frameCount == 0) ? 0 : (g_frameCounter % anim->frameCount);
  const uint8_t *frame = anim->frames[frameIndex];

  const int x = (SCREEN_WIDTH - anim->width) / 2;
  const int y = (SCREEN_HEIGHT - anim->height) / 2;
  display.drawBitmap(x, y, frame, anim->width, anim->height, SSD1306_WHITE);

  if (zeigeFeuchtigkeit)
  {
    display.setTextSize(1);
    display.setTextColor(WHITE);
    display.setCursor(100, 0);
    display.println(messwert_feuchtigkeit);
    display.setCursor(100, 9);
    display.println(String(prozent_feuchtigkeit) + "%");

    display.setCursor(0, 0);
    display.println(messwert_licht);
    display.setCursor(0, 9);
    display.println(String(prozent_licht) + "%");
  }

  display.display();
}